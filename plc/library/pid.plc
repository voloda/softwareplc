(* *************************************** *)
(* PLC library                             *)
(* *************************************** *)
(* PID controler                           *)
(* Version: $Revision: 1.1 $                     *)
(* Last change: $Date                      *)
(* Author: Voloda <Vladimir.Kloz@dtg.cz>   *)
(* *************************************** *)
(* Inputs:                                 *)
(*  P - proportional constant of controler *)
(*  I - integral constant of controler     *)
(*  D - derivative constant of controler   *)
(*  PV - current process variable          *)
(*                                         *)
(* Outputs:                                *)
(*  AV - action variable                   *)
(* *************************************** *)
FUNCTION_BLOCK	pid
	VAR_INPUT
		P: REAL := 0;
		I: REAL := 0;
		D: REAL := 0;
		ISAT: DINT := 16#0fff_ffff_ffff_ffff; 
		PV: REAL := 0;
	END_VAR
	VAR_OUTPUT
		AV: REAL := 0; (* Action Variable *)
	END_VAR
	VAR
		OLD_PV: REAL := 0;
		INTEGRATION: REAL := 0;
		TMPSAT: REAL;
	END_VAR

	(* Calculate P part of controler *)
	LD	P
	MUL	PV
	ST	AV

	(* Calculate D part of controler *)
	LD	PV
	SUB	OLD_PV
	MUL	D
	ADD	AV
	ST	AV

	(* Calculate I part of controler *)
	LD	PV
	ADD	INTEGRATION
	ST	INTEGRATION

	LE	ISAT
	JMPC	test_minsat

	LD	0

	JMP	compute_int

test_minsat:	
	LD	ISAT
	MUL	-1
	ST	TMPSAT

	LE	INTEGRATION
	JMPC	compute_int

	LD	0
compute_int:
	ST	INTEGRATION

	MUL	I

	(* Compute sum for controler output *)
	ADD	AV

	ST	AV

	LD	PV
	ST	OLD_PV
END_FUNCTION_BLOCK

